plugins {
    id "com.jfrog.bintray" version "1.7.3"
}
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group = 'at.allaboutapps.web'
version = '0.1.0'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    archivesBaseName = 'a3webview'

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 25
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

bintray {
    user = 'allaboutapps'
    // read pw from secure travis.yml
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'A3-Android'
        name = "$project.group.$archivesBaseName"

        version {
            name = project.version
            desc = ''
            vcsTag = System.getenv('TRAVIS_TAG')
        }

        licenses = ['MIT']
        vcsUrl = 'https://github.com/allaboutapps/A3WebView'
        websiteUrl = 'http://www.allaboutapps.at'
    }
    configurations = ['archives']
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateSourcesJar
}

dependencies {
    compile "com.android.support:appcompat-v7:$supportLibVersion"
}